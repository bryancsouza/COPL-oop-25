---
path: '/part-15/5-structure-of-haskell'
title: 'The Structure of a Haskell Program'
hidden: false
---

<text-box variant='learningObjectives' name="Learning objectives">

After this section

- You understand how a basic Haskell program is constructed
</text-box>

Here’s a simple Haskell program that does some arithmetic and prints some values.
```Haskell
module Gold where

-- The golden ratio
phi :: Double
phi = (sqrt 5 + 1) / 2

polynomial :: Double -> Double
polynomial x = x^2 - x - 1

f x = polynomial (polynomial x)

main = do
    print (polynomial phi)
    print (f phi)
```

If you put this in a file called `Gold.hs` and run it with (for example) `stack runhaskell Gold.hs`, you should see this output
<sample-output>
    0.0
    -1.0
</sample-output>


Let’s walk through the file.
```Haskell
module Gold where
```
There is one Haskell _module_ per source file. A module consists of _definitions_.
```Haskell
-- The golden ratio
```
This is a comment. Comments are not part of the actual program, but text for human readers of the program.
It is similar to using `# comment` in python.
```Haskell
phi :: Double
phi = (sqrt 5 + 1) / 2
```
This is a definition of the constant `phi`, with an accompanying _type annotation_ (also known as a _type signature_) `phi :: Double`. The type annotation means that `phi` has type `Double`. The line with a equals sign (`=`) is called an _equation_. The left hand side of the `=` is the expression we are defining, and the right hand side of the `=` is the definition.

In general a definition (of a function or constant) consists of an optional _type annotation_ and one or more _equations_

```Haskell
polynomial :: Double -> Double
polynomial x = x^2 - x - 1
```
This is the definition of a function called `polynomial`. It has a type annotation and an equation. Note how an equation for a function differs from the equation of a constant by the presence of a parameter `x` left of the `=` sign. Note also that `^` is the power operator in Haskell, not bitwise xor like in many other languages.

```Haskell
f x = polynomial (polynomial x)
```
This is the definition of a function called `f`. Note the lack of type annotation. What is the type of `f`?

```Haskell
main = do
    print (polynomial phi)
    print (f phi)
```
This is a description of what happens when you run the program.
