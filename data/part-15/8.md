---
path: '/part-15/8s-all-together'
title: 'All Together Now!'
hidden: false
---


<text-box variant='learningObjectives' name="Learning objectives">

After this section

- You have seen a more complicated Haskell module
</text-box>

![source: https://xkcd.com/710/](collatz_conjecture.png)


Finally, here’s a complete Haskell module that uses ifs, pattern matching, local defintions and recursion. The module is interested in the [_Collatz conjecture_](https://en.wikipedia.org/wiki/Collatz_conjecture), a famous open problem in mathematics. It asks:

> Does the Collatz sequence eventually reach 1 for all positive integer initial values?

The Collatz sequence is defined by taking any number as a starting value, and then repeatedly performing the following operation:

*   if the number is even, divide it by two
*   if the number is odd, triple it and add one

As an example, the Collatz sequence for 3 is: 3, 10, 5, 16, 8, 4, 2, 1, 4, 2, 1, 4, 2, 1 … As you can see, once the number reaches 1, it gets caught in a loop.
```Haskell
module Collatz where

-- one step of the Collatz sequence
step :: Integer -> Integer
step x = if even x then down else up
    where down = div x 2
        up = 3*x+1

-- collatz x computes how many steps it takes for the Collatz sequence
-- to reach 1 when starting from x
collatz :: Integer -> Integer
collatz 1 = 0
collatz x = 1 + collatz (step x)

-- longest finds the number with the longest Collatz sequence for initial values
-- between 0 and upperBound
longest :: Integer -> Integer
longest upperBound = longest' 0 0 upperBound

-- helper function for longest
longest' :: Integer -> Integer -> Integer -> Integer
-- end of recursion, return longest length found
longest' number _ 0 = number
-- recursion step: check if n has a longer Collatz sequence than the current known longest
longest' number maxlength n =
    if len > maxlength
    then longest' n len (n-1)
    else longest' number maxlength (n-1)
    where len = collatz n
```

We can load the program in GHCi and play with it.

```Haskell
$ stack ghci
GHCi, version 9.2.8: https://www.haskell.org/ghc/  :? for help
Prelude> :load Collatz.hs
[1 of 1] Compiling Collatz          ( Collatz.hs, interpreted )
Ok, one module loaded.
*Collatz>
```

Let’s verify that our program computes the start of the Collatz sequence for 3 correctly.

```Haskell
*Collatz> step 3
10
*Collatz> step 10
5
*Collatz> step 5
16
```

How many steps does it take for 3 to reach 1?

```Haskell
*Collatz> collatz 3
7
```

What’s the longest Collatz sequence for a starting value under 10? What about 100?

```Haskell
*Collatz> longest 10
9
*Collatz> longest 100
97
```

The lengths of these Collatz sequences are:

```Haskell
*Collatz> collatz 9
19
*Collatz> collatz 97
118
```

With this information, we can finish the rest of the exercises:


## Exercises:

All exercises for this chapter can be found in [Set1](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set1.hs).

Remember that you can test your functions by using
```Haskell
stack ghci Set1.hs
```

Then, you can run the test by using
```Bash
stack runhaskell Set1Test.hs
```
You should be able to make the following exercises now:

<text-box variant='exercise' name="Exercise 1.8">

```Haskell
-- Ex 8: define a function isZero that returns True if it is given an
-- Integer that is 0, and False otherwise. Give isZero a type signature.
--
-- Use pattern matching! Don't use comparisons!
--
-- Ps. remember, the type of booleans in haskell is Bool

isZero = todo
```

</text-box>


<text-box variant='exercise' name="Exercise 1.9">

```Haskell
-- Ex 9: implement using recursion a function sumTo such that
--   sumTo n
-- computes the sum 1+2+...+n

sumTo :: Integer -> Integer
sumTo = todo
```

</text-box>

<text-box variant='exercise' name="Exercise 1.10">

```Haskell
-- Ex 10: power n k should compute n to the power k (i.e. n^k)
-- Use recursion.

power :: Integer -> Integer -> Integer
power = todo
```

</text-box>

<text-box variant='exercise' name="Exercise 1.11">

```Haskell
-- Ex 11: ilog3 n should be the number of times you can divide given
-- number by three (rounding down) before you get 0.
--
-- For example, ilog3 20 ==> 3 since
--   20/3 = 6.66 (gets rounded down to 6)
--   6/3 = 2
--   2/3 = 0.666 (gets rounded down to 0)
--
-- Use recursion to define ilog3. Use the function "div" for integer
-- division. It rounds down for you.
--
-- More examples:
--   ilog3 2 ==> 1
--   ilog3 7 ==> 2

ilog3 :: Integer -> Integer
ilog3 = todo

```

</text-box>


