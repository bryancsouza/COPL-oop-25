---
path: '/part-16/6-the-case-of-expression'
title: 'The case-of Expression'
hidden: false
---



We’ve seen pattern matching in function arguments, but there’s also a way to pattern match in an expression. It looks like this:

```haskell
case <value> of <pattern> -> <expression>
                <pattern> -> <expression>
```

As an example let’s rewrite the `describe` example from the first lecture using `case`:

```haskell
describe :: Integer -> String
describe 0 = "zero"
describe 1 = "one"
describe 2 = "an even prime"
describe n = "the number " ++ show n

describe :: Integer -> String
describe n = case n of 0 -> "zero"
                        1 -> "one"
                        2 -> "an even prime"
                        n -> "the number " ++ show n
```
A more interesting example is when the value we’re pattern matching on is not a function argument. For example:

```haskell
-- parse country code into country name, returns Nothing if code not recognized
parseCountry :: String -> Maybe String
parseCountry "FI" = Just "Finland"
parseCountry "SE" = Just "Sweden"
parseCountry _ = Nothing

flyTo :: String -> String
flyTo countryCode = case parseCountry countryCode of Just country -> "You're flying to " ++ country
                                                        Nothing -> "You're not flying anywhere"

Prelude> flyTo "FI"
"You're flying to Finland"
Prelude> flyTo "DE"
"You're not flying anywhere"
```
We could write the `flyTo` function using a helper function for pattern matching instead of using the case-of expression:

```haskell
flyTo :: String -> String
flyTo countryCode = handleResult (parseCountry countryCode)
    where handleResult (Just country) = "You're flying to " ++ country
        handleResult Nothing        = "You're not flying anywhere"
```
In fact, a case-of expression can always be replaced with a helper function. Here’s one more example, written in both ways:

```haskell
-- given a sentence, decide whether it is a statement, question or exclamation
sentenceType :: String -> String
sentenceType sentence = case last sentence of '.' -> "statement"
                                                '?' -> "question"
                                                '!' -> "exclamation"
                                                _   -> "not a sentence"

-- same function, helper function instead of case-of
sentenceType sentence = classify (last sentence)
    where classify '.' = "statement"
        classify '?' = "question"
        classify '!' = "exclamation"
        classify _   = "not a sentence"

Prelude> sentenceType "This is Haskell."
"statement"
Prelude> sentenceType "This is Haskell!"
"exclamation"
```

### When to Use Case Expressions

You might be asking, what is the point of having another pattern matching syntax. Well, `case` expressions have some advantages over equations which we’ll discuss next.

Firstly, and perhaps most importantly, `case` expressions enable us to pattern match against function outputs. We might want to write early morning motivational messages to working (lazy) Haskellers:

```haskell
motivate :: String -> String
motivate "Monday"    = "Have a nice week at work!"
motivate "Tuesday"   = "You're one day closer to weekend!"
motivate "Wednesday" = "3 more day(s) until the weekend!"
motivate "Thursday"  = "2 more day(s) until the weekend!"
motivate "Friday"    = "1 more day(s) until the weekend!"
motivate _           = "Relax! You don't need to work today!"
```

Using a `case` expression we can run a helper function against the argument and pattern match on the result:

```haskell
motivate :: String -> String
motivate day = case distanceToSunday day of
    6 -> "Have a nice week at work!"
    5 -> "You're one day closer to weekend!"
    n -> if n > 1
        then show (n - 1) ++ " more day(s) until the weekend!"
        else "Relax! You don't need to work today!"
```

By the way, there’s also a third way, guards:

```haskell
motivate :: String -> String
motivate day
    | n == 6 = "Have a nice week at work!"
    | n == 5 = "You're one day closer to weekend!"
    | n > 1 = show (n - 1) ++ " more day(s) until the weekend!"
    | otherwise = "Relax! You don't need to work today!"
    where n = distanceToSunday day
```
We’ll see in a moment how we can define `distanceToSunday` using equations and `case` expressions.

Secondly, if a helper function needs to be shared among many patterns, then equations don’t work. For example:

```haskell
area :: String -> Double -> Double
area "square" x = square x
area "circle" x = pi * square x
    where square x = x * x
```

This won’t compile because a the `where` clause only appends to the `"circle"` case, so the `square` helper function is not available in the `"square"` case. On the other hand, we can write


```haskell
area :: String -> Double -> Double
area shape x = case shape of
    "square" -> square x
    "circle" -> pi * square x
    where square x = x*x
```


Thirdly, `case` expressions may help to write more concise code in a situation where a (long) function name would have to be repeated multiple times using equations. As we saw above, we might need a function which measures the distance between a given day and Sunday:

```haskell
distanceToSunday :: String -> Int
distanceToSunday "Monday"    = 6
distanceToSunday "Tuesday"   = 5
distanceToSunday "Wednesday" = 4
distanceToSunday "Thursday"  = 3
distanceToSunday "Friday"    = 2
distanceToSunday "Saturday"  = 1
distanceToSunday "Sunday"    = 0
```

Using a `case` expression leads into much more concise implementation:

```haskell
distanceToSunday :: String -> Int
distanceToSunday d = case d of
    "Monday"    -> 6
    "Tuesday"   -> 5
    "Wednesday" -> 4
    "Thursday"  -> 3
    "Friday"    -> 2
    "Saturday"  -> 1
    "Sunday"    -> 0
```
These three benefits make the `case` expression a versatile tool in a Haskeller’s toolbox. It’s worth remembering how `case` works.

(Representing weekdays as strings may get the job done, but it’s not the perfect solution. What happens if we apply `motivate` to `"monday"` (with all letters in lower case) or `"keskiviikko"`? In Lecture 5, we will learn a better way to represent things like weekdays.)

## Exercises

All exercises can be found in [Set2a](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set2a.hs)
and [Set2b](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set2b.hs). Please pay attention in the title of the exercise in which file the exercises of this section can be found.

## Exercises from 2a:

None for now :)!

## Exercises from 2b:

<text-box variant='exercise' name="Exercise 2b.5">

```Haskell
-- Ex 5: let's make a countdown for a rocket! Given a number, you
-- should produce a string that says "Ready!", counts down from the
-- number, and then says "Liftoff!".
--
-- For example,
--   countdown 4 ==> "Ready! 4... 3... 2... 1... Liftoff!"
--
-- Hints:
-- * you can combine strings with the ++ operator
-- * you can use the show function to convert a number into a string
-- * you'll probably need a recursive helper function

countdown :: Integer -> String
countdown = todo
```
</text-box>




<text-box variant='exercise' name="Exercise 2b.6">

```Haskell
-- Ex 6: implement the function smallestDivisor that returns the
-- smallest number (greater than 1) that divides the given number evenly.
--
-- That is, when
--   smallestDivisor n ==> k
-- we have
--   n = t*k
-- for some t.
--
-- Ps. your function doesn't need to work for inputs 0 and 1, but
-- remember this in the next exercise!
--
-- Hint: remember the mod function!

smallestDivisor :: Integer -> Integer
smallestDivisor = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 2b.7">

```Haskell
-- Ex 7: implement a function isPrime that checks if the given number
-- is a prime number. Use the function smallestDivisor.
--
-- Ps. 0 and 1 are not prime numbers

isPrime :: Integer -> Bool
isPrime = todo
```
</text-box>


<text-box variant='exercise' name="Exercise 2b.8">

```Haskell
-- Ex 8: implement a function biggestPrimeAtMost that returns the
-- biggest prime number that is less than or equal to the given
-- number. Use the function isPrime you just defined.
--
-- You don't need to care about arguments less than 2. Any behaviour
-- for them is fine.
--
-- Examples:
--   biggestPrimeAtMost 3 ==> 3
--   biggestPrimeAtMost 10 ==> 7

biggestPrimeAtMost :: Integer -> Integer
biggestPrimeAtMost = todo
```
</text-box>





