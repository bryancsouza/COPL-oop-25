---
path: '/part-19/3-recursive-types'
title: 'Recursive Types'
hidden: false
---

<text-box variant='learningObjectives' name="Learning objectives">

After this section

*   You know how to define a recursive data type
*   You know in particular how to use a tree in Haskell

</text-box>


So far, all of the types we’ve defined have been of constant size. We can represent one report or one colour, but how could we represent a collection of things? We could use lists of course, but could we define a list type ourselves?

Just like Haskell functions, Haskell data types can be _recursive_. This is no weirder than having an object in Java or Python that refers to another object of the same class. This is how you define a list of integers:

```haskell
data IntList = Empty | Node Int IntList
    deriving Show

ihead :: IntList -> Int
ihead (Node i _) = i

itail :: IntList -> IntList
itail (Node _ t) = t

ilength :: IntList -> Int
ilength Empty = 0
ilength (Node _ t) = 1 + ilength t
```

We can use the functions defined above to work with lists of integers:

```haskell
Prelude> ihead (Node 3 (Node 5 (Node 4 Empty)))
3
Prelude> itail (Node 3 (Node 5 (Node 4 Empty)))
Node 5 (Node 4 Empty)
Prelude> ilength (Node 3 (Node 5 (Node 4 Empty)))
3
```
Note that we can’t put values other than `Int`s inside our `IntList`:

```
Prelude> Node False Empty

<interactive>:3:6: error:
    • Couldn't match expected type ‘Int’ with actual type ‘Bool’
    • In the first argument of ‘Node’, namely ‘False’
        In the expression: Node False Empty
        In an equation for ‘it’: it = Node False Empty
```

To be able to put any type of element in our list, let’s do the same thing with a type parameter. This is the same as the built in type `[a]`, but with slightly clunkier syntax:

```
data List a = Empty | Node a (List a)
    deriving Show
```

Note how we need to pass the the type parameter `a` onwards in the recursion. We need to write `Node a (List a)` instead of `Node a List`. The `Node` constructor has two arguments. The first has type `a`, and the second has type `List a`. Here are the reimplementations of some standard list functions for our `List` type:

```haskell
lhead :: List a -> a
lhead (Node h _) = h

ltail :: List a -> List a
ltail (Node _ t) = t

lnull :: List a -> Bool
lnull Empty = True
lnull _     = False

llength :: List a -> Int
llength Empty = 0
llength (Node _ t) = 1 + llength t

Prelude> lhead (Node True Empty)
True
Prelude> ltail (Node True (Node False Empty))
Node False Empty
Prelude> lnull Empty
True
```

Note that just like with normal Haskell lists, we can’t have elements of different types in the same list:

```haskell
Prelude> Node True (Node "foo" Empty)

<interactive>:5:12: error:
    • Couldn't match type ‘[Char]’ with ‘Bool’
        Expected type: List Bool
        Actual type: List [Char]
    • In the second argument of ‘Node’, namely ‘(Node "foo" Empty)’
        In the expression: Node True (Node "foo" Empty)
        In an equation for ‘it’: it = Node True (Node "foo" Empty)
```

### Example: Growing a Tree

Just like a list, we can also represent a binary tree:
```haskell
data Tree a = Node a (Tree a) (Tree a) | Empty
```
Our tree contains nodes, which contain a value of type `a` and two child trees, and empty trees.

In case you’re not familiar with binary trees, they’re a data structure that’s often used as the basis for other data structures (`Data.Map` is based on trees!). Binary trees are often drawn as (upside-down) pictures, like this:

![](binaryTree.svg)

The highest node in the tree is called the _root_ (`0` in this case), and the nodes with no children are called `leaves` (`2`, `3` and `4` in this case). We can define this tree using our `Tree` type like this:

```haskell
example :: Tree Int
example = (Node 0 (Node 1 (Node 2 Empty Empty)
                            (Node 3 Empty Empty))
                    (Node 4 Empty Empty))
```
The height of a binary tree is length of the longest path from the root to a leaf. In Haskell terms, it’s how many nested levels of `Node` constructors you need to build the tree. The height of our example tree is 3. Here’s a function that computes the height of a tree:

```haskell
treeHeight :: Tree a -> Int
treeHeight Empty = 0
treeHeight (Node _ l r) = 1 + max (treeHeight l) (treeHeight r)

treeHeight Empty ==> 0
treeHeight (Node 2 Empty Empty)
    ==> 1 + max (treeHeight Empty) (treeHeight Empty)
    ==> 1 + max 0 0
    ==> 1
treeHeight (Node 1 Empty (Node 2 Empty Empty))
    ==> 1 + max (treeHeight Empty) (treeHeight (Node 2 Empty Empty))
    ==> 1 + max 0 1
    ==> 2
treeHeight (Node 0 (Node 1 Empty (Node 2 Empty Empty)) Empty)
    ==> 1 + max (treeHeight (Node 1 Empty (Node 2 Empty Empty))) (treeHeight Empty)
    ==> 1 + max 2 0
    ==> 3
```

In case you’re familiar with _binary search trees_, here are the definitions of the lookup and insert opertions for a binary search tree. If you don’t know what I’m talking about, you don’t need to understand this.

```haskell
lookup :: Int -> Tree Int -> Bool
lookup x Empty = False
lookup x (Node y l r)
    | x < y = lookup x l
    | x > y = lookup x r
    | otherwise = True

insert :: Int -> Tree Int -> Tree Int
insert x Empty = Node x Empty Empty
insert x (Node y l r)
    | x < y = Node y (insert x l) r
    | x > y = Node y l (insert x r)
    | otherwise = Node y l r
```

## Exercises

All exercises can be found in [Set5a](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set5a.hs)
and [Set5b](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set5b.hs). Please pay attention in the title of the exercise in which file the exercises of this section can be found.

# Optional:
The assignments of 5b are optional. Only make them if you want some extra practice on recursive datatypes, but trees will not be asked on the exam.

## Exercises from 5b:

<text-box variant='exercise' name="Exercise 5b.1">

```Haskell
-- Ex 1: implement the function valAtRoot which returns the value at
-- the root (top-most node) of the tree. The return value is Maybe a
-- because the tree might be empty (i.e. just a Empty)

valAtRoot :: Tree a -> Maybe a
valAtRoot t = todo
```
</text-box>


<text-box variant='exercise' name="Exercise 5b.2">

```Haskell
-- Ex 2: compute the size of a tree, that is, the number of Node
-- constructors in it
--
-- Examples:
--   treeSize (Node 3 (Node 7 Empty Empty) Empty)  ==>  2
--   treeSize (Node 3 (Node 7 Empty Empty) (Node 1 Empty Empty))  ==>  3

treeSize :: Tree a -> Int
treeSize t = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.3">

```Haskell
-- Ex 3: get the largest value in a tree of positive Ints. The
-- largest value of an empty tree should be 0.
--
-- Examples:
--   treeMax Empty  ==>  0
--   treeMax (Node 3 (Node 5 Empty Empty) (Node 4 Empty Empty))  ==>  5

treeMax :: Tree Int -> Int
treeMax = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.4">

```Haskell
-- Ex 4: implement a function that checks if all tree values satisfy a
-- condition.
--
-- Examples:
--   allValues (>0) Empty  ==>  True
--   allValues (>0) (Node 1 Empty (Node 2 Empty Empty))  ==>  True
--   allValues (>0) (Node 1 Empty (Node 0 Empty Empty))  ==>  False

allValues :: (a -> Bool) -> Tree a -> Bool
allValues condition tree = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.5">

```Haskell
-- Ex 5: implement map for trees.
--
-- Examples:
--
-- mapTree (+1) Empty  ==>  Empty
-- mapTree (+2) (Node 0 (Node 1 Empty Empty) (Node 2 Empty Empty))
--   ==> (Node 2 (Node 3 Empty Empty) (Node 4 Empty Empty))

mapTree :: (a -> b) -> Tree a -> Tree b
mapTree f t = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.6">

```Haskell
-- Ex 6: given a value and a tree, build a new tree that is the same,
-- except all nodes that contain the value have been removed. Also
-- remove the subnodes of the removed nodes.
-- Check file for more in depth examples

cull :: Eq a => a -> Tree a -> Tree a
cull val tree = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.7">

```Haskell
-- Ex 7: check if a tree is ordered. A tree is ordered if:
--  * all values to the left of the root are smaller than the root value
--  * all of the values to the right of the root are larger than the root value
--  * and the left and right subtrees are ordered.
--
-- Hint: allValues will help you here!
-- Check file for more in depth examples

isOrdered :: Ord a => Tree a -> Bool
isOrdered = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 5b.8">

```Haskell
-- Ex 8: a path in a tree can be represented as a list of steps that
-- go either left or right.

data Step = StepL | StepR
  deriving (Show, Eq)

-- Define a function walk that takes a tree and a list of steps, and
-- returns the value at that point. Return Nothing if you fall of the
-- tree (i.e. hit a Empty).
--
-- Examples:
--   walk [] (Node 1 (Node 2 Empty Empty) Empty)       ==>  Just 1
--   walk [StepL] (Node 1 (Node 2 Empty Empty) Empty)  ==>  Just 2
--   walk [StepL,StepL] (Node 1 (Node 2 Empty Empty) Empty)  ==>  Nothing

walk :: [Step] -> Tree a -> Maybe a
walk = todo

```
</text-box>

<text-box variant='exercise' name="Exercise 5b.9">

```Haskell
-- Ex 9: given a tree, a path and a value, set the value at the end of
-- the path to the given value. Since Haskell datastructures are
-- immutable, you'll need to build a new tree.
--
-- If the path falls off the tree, do nothing.
-- Check file for more in depth examples

set :: [Step] -> a -> Tree a -> Tree a
set path val tree = todo

```
</text-box>

<text-box variant='exercise' name="Exercise 5b.10`">

```Haskell
-- Ex 10: given a value and a tree, return a path that goes from the
-- root to the value. If the value doesn't exist in the tree, return Nothing.
--
-- You may assume the value occurs in the tree at most once.
--
-- Examples:
--   search 1 (Node 2 (Node 1 Empty Empty) (Node 3 Empty Empty))  ==>  Just [StepL]
--   search 1 (Node 2 (Node 4 Empty Empty) (Node 3 Empty Empty))  ==>  Nothing
--   search 1 (Node 2 (Node 3 (Node 4 Empty Empty)
--                            (Node 1 Empty Empty))
--                    (Node 5 Empty Empty))                     ==>  Just [StepL,StepR]

search :: Eq a => a -> Tree a -> Maybe [Step]
search = todo
```
</text-box>







