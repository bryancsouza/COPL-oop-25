---
path: '/part-18/5-data-maps'
title: 'Data Maps'
hidden: false
---



<text-box variant='learningObjectives' name="Learning objectives">

After this section
*   You know how to use the Data.Map datastructure in Haskell

</text-box>

Now that we are familiar with the standard type classes, we can look at one of their applications: First, the `Map` datastructure

### `Data.Map`

The `Data.Map` module defines the `Map` type. Maps are search trees for key-value pairs. It is similar to the `dictionaries` you have seen in Python.
 One way to look at this is that a value of type `Map k v` is roughly the same as a value of type `[(k,v)]`, a list of pairs. However, the operations on a map are more efficient than operations on a list.

Since `Data.Map` contains some function with the same names as `Prelude` functions, the namespace needs to be imported _qualified_:
```haskell
import qualified Data.Map as Map
```
Now we can refer to the map type as `Map.Map`, and to various map functions like `Map.insert`. Here are the most important functions for maps:

```haskell
-- Create a Map from a list of key-value pairs
Map.fromList :: Ord k => [(k, a)] -> Map.Map k a

-- Insert a value into a map. Overrides any previous value with the same key.
-- Returns a new map. Does not mutate the given map.
Map.insert :: Ord k => k -> a -> Map.Map k a -> Map.Map k a

-- Get a value from a map using a key. Returns Nothing if the key was not present in the map.
Map.lookup :: Ord k => k -> Map.Map k a -> Maybe a

-- An empty map
Map.empty :: Map.Map k a
```

The `Ord` constraint for the key type of the map is needed because maps are implemented as _ordered binary search trees_.

Note that like all Haskell values, maps are _immutable_ meaning you can’t change a map once you define it. However, map operations like `insert` produce a _new_ map. To perform multiple map operations you need to reuse the return value. Here’s a GHCi session operating on a map.

```haskell
Prelude> import qualified Data.Map as Map
Prelude Map> values = Map.fromList [("z",3),("w",4)]
Prelude Map> Map.lookup "z" values
Just 3
Prelude Map> Map.lookup "banana" values
Nothing
Prelude Map> Map.insert "x" 7 values
fromList [("w",4),("x",7),("z",3)]
Prelude Map> values                                       -- note immutability!
fromList [("w",4),("z",3)]
Prelude Map> Map.insert "x" 1 (Map.insert "y" 2 values)   -- two insertions
fromList [("w",4),("x",1),("y",2),("z",3)]
Prelude Map>
```
Here’s an example of representing a bank as a `Map String Int` (map from account name to account balance), and withdrawing some money from an account:

```haskell
withdraw :: String -> Int -> Map.Map String Int -> Map.Map String Int
withdraw account amount bank =
    case Map.lookup account bank of
    Nothing  -> bank                                   -- account not found, no change
    Just sum -> Map.insert account (sum-amount) bank   -- set new balance
```
Here’s how you might use the `withdraw` function in `GHCi`. Note how the maps get printed as `fromList` invocations. Also note how calling `withdraw ... bank` returns a _new_ bank and doesn’t change the existing bank.

```haskell
GHCi> bank = Map.fromList [("Bob",100),("Mike",50)]
GHCi> withdraw "Bob" 80 bank
fromList [("Bob",20),("Mike",50)]
GHCi> bank                         -- note immutability
fromList [("Bob",100),("Mike",50)]
GHCi> withdraw "Bozo" 1000 bank
fromList [("Bob",100),("Mike",50)]
```

`Data.Map` defines all sorts of useful higher-order functions for updating maps. We can rewrite the `withdraw` function using `Data.Map.adjust`:

```haskell
withdraw :: String -> Int -> Map.Map String Int -> Map.Map String Int
withdraw account amount bank = Map.adjust (\x -> x-amount) account bank
```
**Note!** There are separate `Data.Map.Strict` and `Data.Map.Lazy` implementations. When you import `Data.Map` you get `Data.Map.Lazy`. You can find the documentation for all the `Data.Map` functions in [the docs for `Data.Map.Lazy`](https://hackage.haskell.org/package/containers-0.6.5.1/docs/Data-Map-Lazy.html). We won’t go into their differences here, but mostly you should use `Data.Map.Strict` in real code.


## Exercises

All exercises can be found in [Set4a](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set4a.hs)
and [Set4b](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set4b.hs). Please pay attention in the title of the exercise in which file the exercises of this section can be found.

## Exercises from 4a:

<text-box variant='exercise' name="Exercise 4a.6">

```Haskell
-- Ex 6: Implement the function incrementKey, that takes a list of
-- (key,value) pairs, and adds 1 to all the values that have the given key.
--
-- You'll need to add _class constraints_ to the type of incrementKey
-- to make the function work!
--
-- The function needs to be generic and handle all compatible types,
-- see the examples.
--
-- Examples:
--   incrementKey True [(True,1),(False,3),(True,4)] ==> [(True,2),(False,3),(True,5)]
--   incrementKey 'a' [('a',3.4)] ==> [('a',4.4)]

incrementKey :: k -> [(k,v)] -> [(k,v)]
incrementKey = todo
```
</text-box>


<text-box variant='exercise' name="Exercise 4a.7">

```Haskell
-- Ex 7: compute the average of a list of values of the Fractional
-- class.
--
-- There is no need to handle the empty list case.
--
-- Hint! since Fractional is a subclass of Num, you have all
-- arithmetic operations available
--
-- Hint! you can use the function fromIntegral to convert the list
-- length to a Fractional

average :: Fractional a => [a] -> a
average xs = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 4a.8">

```Haskell
-- Ex 8: given a map from player name to score and two players, return
-- the name of the player with more points. If the players are tied,
-- return the name of the first player (that is, the name of the
-- player who comes first in the argument list, player1).
--
-- If a player doesn't exist in the map, you can assume they have 0 points.
--
-- Hint: Map.findWithDefault can make this simpler
--
-- Examples:
--   winner (Map.fromList [("Bob",3470),("Jane",2130),("Lisa",9448)]) "Jane" "Lisa"
--     ==> "Lisa"
--   winner (Map.fromList [("Mike",13607),("Bob",5899),("Lisa",5899)]) "Lisa" "Bob"
--     ==> "Lisa"

winner :: Map.Map String Int -> String -> String -> String
winner scores player1 player2 = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 4a.9">

```Haskell
-- Ex 9: compute how many times each value in the list occurs. Return
-- the frequencies as a Map from value to Int.
--
-- Challenge 1: try using Map.alter for this
--
-- Challenge 2: use foldr to process the list
--
-- Example:
--   freqs [False,False,False,True]
--     ==> Map.fromList [(False,3),(True,1)]

freqs :: (Eq a, Ord a) => [a] -> Map.Map a Int
freqs xs = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 4a.10">

```Haskell
-- Ex 10: recall the withdraw example from the course material. Write a
-- similar function, transfer, that transfers money from one account
-- to another.
--
-- However, the function should not perform the transfer if
-- * the from account doesn't exist,
-- * the to account doesn't exist,
-- * the sum is negative,
-- * or the from account doesn't have enough money.
--
-- Hint: there are many ways to implement this logic. Map.member or
-- Map.notMember might help.
--
-- Examples:
--   let bank = Map.fromList [("Bob",100),("Mike",50)]
--   transfer "Bob" "Mike" 20 bank
--     ==> fromList [("Bob",80),("Mike",70)]
--   transfer "Bob" "Mike" 120 bank
--     ==> fromList [("Bob",100),("Mike",50)]
--   transfer "Bob" "Lisa" 20 bank
--     ==> fromList [("Bob",100),("Mike",50)]
--   transfer "Lisa" "Mike" 20 bank
--     ==> fromList [("Bob",100),("Mike",50)]

transfer :: String -> String -> Int -> Map.Map String Int -> Map.Map String Int
transfer from to amount bank = todo
```
</text-box>







