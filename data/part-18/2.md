---
path: '/part-18/2-folding'
title: 'Folding'
hidden: false
---

<text-box variant='learningObjectives' name="Learning objectives">

After this section
*   Tuples
*   Type classes
*   Data structures: Map, Array

</text-box>


Consider the functions `sumNumbers :: [Int] -> Int`, `myMaximum :: [Int] -> Int`, and `countNothings :: [Maybe a] -> Int` again.

```haskell
sumNumbers :: [Int] -> Int
sumNumbers [] = 0
sumNumbers (x:xs) = x + sumNumbers xs

myMaximum :: [Int] -> Int
myMaximum [] = 0
myMaximum (x:xs) = go x xs
    where go biggest [] = biggest
        go biggest (x:xs) = go (max biggest x) xs

countNothings :: [Maybe a] -> Int
countNothings [] = 0
countNothings (Nothing : xs) = 1 + countNothings xs
countNothings (Just _  : xs) = countNothings xs
```

They have one common characteristic. They take a list and produce a value that depends on the values of the elements in the given list. They “crunch” or _fold_ a list of many values into a single value.

Prelude has a function called `foldr`, which performs a _right associative fold_ over a `Foldable` data type. We’ll learn more about `Foldable` soon. At this point, it suffices to think of lists, so we define

```haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f y []     = y
foldr f y (x:xs) = f x (foldr f y xs)
```

What this definition says, is that for an empty list `[] :: [a]`, `foldr` returns the default value `y :: b`. For any other list `x : xs`, `foldr` applies `f` to `x` and the result of `foldr f y xs` (i.e. folding over the rest of the list). It’s a simple definition by recursion.

In other words, `foldr` calls its argument function `f` repeatedly with two arguments.

*   The first argument is the current element from the list.
*   The second argument is what `f` returned for the rest of the list.

Consider the list `[1,2,3]`:

![](list123.svg)

The expression `foldr (+) 0 [1,2,3]` evaluates as follows:

```haskell
foldr (+) 0 [1,2,3] ==> foldr (+) 0 (1:2:3:[])
                    ==> 1 + (foldr (+) 0 (2:3:[]))
                    ==> 1 + (2 + (foldr (+) 0 (3:[])))
                    ==> 1 + (2 + (3 + (foldr (+) 0 [])))
                    ==> 1 + (2 + (3 + 0))
```

The result can be thought of as a tree:

![](sum123.svg)

One way to think about `foldr f y xs` is that it replaces the `(:)` operation with `f` and `[]` with `y`. In this case, `f` was `(+)` and `y` was `0`. If you write out how `sumNumbers [1,2,3]` behaves, you’ll notice that it performs the same computation as `foldr (+) 0 [1,2,3]` does! More generally:

    sumNumbers xs == foldr (+) 0 xs

Those more experienced with math may notice that we can prove this claim by _induction_: Firstly, `sumNumbers [] ==> 0` and `foldr (+) 0 [] ==> 0`, so in the base case `sumNumbers [] == foldr (+) 0 []`. Next, we may assume as our induction hypothesis that `sumNumbers xs == foldr (+) 0 xs` for any list `xs`. Then, for the list `x:xs`, we have `sumNumbers (x:xs) ==> x + sumNumbers xs`. Hence, `foldr (+) 0 (x:xs) ==> x + foldr (+) 0 xs ==> x + sumNumbers xs` by induction hypothesis. Therefore, by induction, the equation holds.

You don’t need to read, write, or understand induction proofs in this course, but perhaps it is reassuring to know that properties and equalities of functions in Haskell can be (in principle) analysed mathematically, because Haskell is such a nice language. (Equalities and properties can be analysed in any programming language, but for Haskell, this analysis is especially convenient because Haskell is pure.)

Another folding example is the `map` function:

```haskell
map g xs = foldr helper [] xs
    where helper y ys = g y : ys
```

To see why this works, consider what `foldr helper [] [x1,x2,..,xn]` does:

![](foldr1.svg)

Now, since `helper x xs ==> g x : xs` for every `x` and `xs`, we get that:

![](foldr2.svg)

The resulting list, `[ g x1, g x2, g x3, ..., g xn ]`, is then exactly what we would have gotten with `map g xs`. (This could have been also proved by induction as we did for `sumNumbers`.) The lesson to take away is that folding is a particular, yet quite general, way to apply some transformation recursively into some structure (e.g. a list).



## Exercises

All exercises can be found in [Set4a](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set4a.hs)
and [Set4b](https://github.com/moocfi/haskell-mooc/blob/master/exercises/Set4b.hs). Please pay attention in the title of the exercise in which file the exercises of this section can be found.

## Exercises from 4a:
None for now :)

## Exercises from 4b:

<text-box variant='exercise' name="Exercise 4b.1">

```Haskell
-- Ex 1: countNothings with a fold. The function countNothings from
-- the course material can be implemented using foldr. Your task is to
-- define countHelper so that the following definition of countNothings
-- works.
--
-- Hint: You can start by trying to add a type signature for countHelper.
--
-- Challenge: look up the maybe function and use it in countHelper.
--
-- Examples:
--   countNothings []  ==>  0
--   countNothings [Just 1, Nothing, Just 3, Nothing]  ==>  2

countNothings :: [Maybe a] -> Int
countNothings xs = foldr countHelper 0 xs

countHelper = todo
```
</text-box>


<text-box variant='exercise' name="Exercise 4b.2">

```Haskell
-- Ex 2: myMaximum with a fold. Just like in the previous exercise,
-- define maxHelper so that the given definition of myMaximum works.
--
-- Examples:
--   myMaximum []  ==>  0
--   myMaximum [1,3,2]  ==>  3

myMaximum :: [Int] -> Int
myMaximum [] = 0
myMaximum (x:xs) = foldr maxHelper x xs

maxHelper = todo
```
</text-box>



<text-box variant='exercise' name="Exercise 4b.3">

```Haskell
-- Ex 3: compute the sum and length of a list with a fold. Define
-- slHelper and slStart so that the given definition of sumAndLength
-- works. This could be used to compute the average of a list.
--
-- Start by giving slStart and slHelper types.
--
-- Examples:
--   sumAndLength []             ==>  (0.0,0)
--   sumAndLength [1.0,2.0,4.0]  ==>  (7.0,3)


sumAndLength :: [Double] -> (Double,Int)
sumAndLength xs = foldr slHelper slStart xs

slStart = todo
slHelper = todo

```
</text-box>


<text-box variant='exercise' name="Exercise 4b.4">

```Haskell
-- Ex 4: implement concat with a fold. Define concatHelper and
-- concatStart so that the given definition of myConcat joins inner
-- lists of a list.
--
-- Examples:
--   myConcat [[]]                ==> []
--   myConcat [[1,2,3],[4,5],[6]] ==> [1,2,3,4,5,6]

myConcat :: [[a]] -> [a]
myConcat xs = foldr concatHelper concatStart xs

concatStart = todo
concatHelper = todo

```
</text-box>


<text-box variant='exercise' name="Exercise 4b.5">

```Haskell
-- Ex 5: get all occurrences of the largest number in a list with a
-- fold. Implement largestHelper so that the given definition of largest works.
--
-- Examples:
--   largest [] ==> []
--   largest [1,3,2] ==> [3]
--   largest [1,3,2,3] ==> [3,3]

largest :: [Int] -> [Int]
largest xs = foldr largestHelper [] xs

largestHelper = todo

```
</text-box>


<text-box variant='exercise' name="Exercise 4b.6">

```Haskell
-- Ex 6: get the first element of a list with a fold. Define
-- headHelper so that the given definition of myHead works.
--
-- Start by giving headHelper a type.
--
-- Examples:
--   myHead []  ==>  Nothing
--   myHead [1,2,3]  ==>  Just 1

myHead :: [a] -> Maybe a
myHead xs = foldr headHelper Nothing xs

headHelper = todo
```
</text-box>

<text-box variant='exercise' name="Exercise 4b.7">

```Haskell
-- Ex 7: get the last element of a list with a fold. Define lasthelper
-- so that the given definition of myLast works.
--
-- Start by giving lastHelper a type.
--
-- Examples:
--   myLast [] ==> Nothing
--   myLast [1,2,3] ==> Just 3

myLast :: [a] -> Maybe a
myLast xs = foldr lastHelper Nothing xs

lastHelper = todo
```
</text-box>


